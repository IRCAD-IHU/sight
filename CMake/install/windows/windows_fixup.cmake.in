#This script is called at install time only on Windows OS
#It will be configured at configure time, only '@' variables will be replaced.


#function to find libs and Bundles/* directories
function(findFwlibsAndBundles FWLIBS_BUNDLES_DIRECTORIES)
    file(GLOB_RECURSE LIBS @CMAKE_INSTALL_PREFIX@/*@CMAKE_SHARED_LIBRARY_SUFFIX@)
    list(REMOVE_DUPLICATES LIBS)
    set(FOLDERS)
    foreach(LIB ${LIBS})
        get_filename_component(LIB_DIR "${LIB}" DIRECTORY)
        list(APPEND FOLDERS ${LIB_DIR})
    endforeach()
    list(REMOVE_DUPLICATES FOLDERS)
    set(${FWLIBS_BUNDLES_DIRECTORIES} ${FOLDERS} PARENT_SCOPE)
endfunction()


# Run the script when installing but not a second time when running CPack
if ( NOT ${CMAKE_INSTALL_PREFIX} MATCHES "/_CPack_Packages/.*/(TGZ|ZIP)/")

    #Find all /*/.dll in /Bundles /plugins /share
    file(GLOB_RECURSE BUNDLES FOLLOW_SYMLINKS "@CMAKE_INSTALL_PREFIX@/@BUNDLE_LIB_PREFIX@/*@CMAKE_SHARED_LIBRARY_SUFFIX@")
    file(GLOB_RECURSE PLUGINS FOLLOW_SYMLINKS "@CMAKE_INSTALL_PREFIX@/plugins/*@CMAKE_SHARED_LIBRARY_SUFFIX@")
    file(GLOB_RECURSE SHARE FOLLOW_SYMLINKS "@CMAKE_INSTALL_PREFIX@/share/*@CMAKE_SHARED_LIBRARY_SUFFIX@")

    include(BundleUtilities)

    set(LIBS)
    list(APPEND LIBS ${BUNDLES} ${PLUGINS})
    #Look for all directories containing '.dll' files

    findFwlibsAndBundles(FWLIBS_BUNDLES_DIRECTORIES)
    list(APPEND FWLIBS_BUNDLES_DIRECTORIES @EXTERNAL_LIBRARIES_DIRECTORIES@)

    #Call fixup bundle on the fwlauncher and on all 'plugins' libraries (like bundle, plugins and share)
    fixup_bundle("@CMAKE_INSTALL_PREFIX@/@LAUNCHER_PATH@" "${LIBS}" "${FWLIBS_BUNDLES_DIRECTORIES}")

endif()
