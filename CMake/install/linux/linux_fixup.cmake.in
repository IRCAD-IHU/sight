#This script is called at install time only on linux OS
#It will be configured at configure time, only '@' variables will be replaced.
#function to find libs and Bundles/* directories

function(findFwlibsAndBundles FWLIBS_BUNDLES_DIRECTORIES)
    file(GLOB_RECURSE LIBS @CMAKE_INSTALL_PREFIX@/@FWBUNDLE_LIB_PREFIX@/*@CMAKE_SHARED_LIBRARY_SUFFIX@*)
    if(@FW_BUILD_EXTERNAL@)
        file(GLOB_RECURSE LIBS @Sight_LIBRARY_DIR@/*@CMAKE_SHARED_LIBRARY_SUFFIX@*)
    endif()

    list(REMOVE_DUPLICATES LIBS)
    set(FOLDERS)
    foreach(LIB ${LIBS})
        get_filename_component(LIB_DIR "${LIB}" DIRECTORY)
        list(APPEND FOLDERS ${LIB_DIR})
    endforeach()
    if(FOLDERS)
        list(REMOVE_DUPLICATES FOLDERS)
    endif()
    set(${FWLIBS_BUNDLES_DIRECTORIES} ${FOLDERS} PARENT_SCOPE)
endfunction()

# Run the script when installing but not a second time when running CPack
if ( NOT ${CMAKE_INSTALL_PREFIX} MATCHES "/_CPack_Packages/.*/(TGZ|ZIP)/")

    #Find all /*/.so* in /Bundles /plugins
    file(GLOB_RECURSE BUNDLES FOLLOW_SYMLINKS "@CMAKE_INSTALL_PREFIX@/@FWBUNDLE_LIB_PREFIX@/*@CMAKE_SHARED_LIBRARY_SUFFIX@*")
    file(GLOB_RECURSE PLUGINS FOLLOW_SYMLINKS "@CMAKE_INSTALL_PREFIX@/plugins/*@CMAKE_SHARED_LIBRARY_SUFFIX@*")

    include(BundleUtilities)
    set(LIBS "")
    list(APPEND LIBS ${BUNDLES} ${PLUGINS})
    # Also add bundles from the SDK
    if(@FW_BUILD_EXTERNAL@)
        #file(GLOB_RECURSE BUNDLES FOLLOW_SYMLINKS "@Sight_LIBRARY_DIR@/*@CMAKE_SHARED_LIBRARY_SUFFIX@*")

        foreach(REQUIREMENT @PROJECT_REQUIREMENTS@)
            file(GLOB_RECURSE BUNDLES FOLLOW_SYMLINKS "@Sight_LIBRARY_DIR@/${REQUIREMENT}*/*@CMAKE_SHARED_LIBRARY_SUFFIX@*")
            if(BUNDLES)
                list(APPEND LIBS ${BUNDLES})
            endif()
        endforeach()

    endif()

    set(USE_SYSTEM_LIB @USE_SYSTEM_LIB@)
    if(NOT USE_SYSTEM_LIB)
        #Look for all directories containing '.so*' files
        findFwlibsAndBundles(FWLIBS_BUNDLES_DIRECTORIES)
        list(APPEND FWLIBS_BUNDLES_DIRECTORIES @EXTERNAL_LIBRARIES_DIRECTORIES@)
        list(APPEND FWLIBS_BUNDLES_DIRECTORIES @CMAKE_INSTALL_PREFIX@/lib)

        if(@FW_BUILD_EXTERNAL@)
            list(APPEND FWLIBS_BUNDLES_DIRECTORIES @Sight_LIBRARY_DIR@)
        endif()
        set(LAUNCHER_PATH @CMAKE_INSTALL_PREFIX@/@LAUNCHER_PATH@)

        #Call fixup bundle on the fwlauncher and on all 'plugins' libraries (like bundle, plugins and share)
        fixup_bundle("${LAUNCHER_PATH}" "${LIBS}" "${FWLIBS_BUNDLES_DIRECTORIES}")
    endif()

    #Move all libraries in 'lib' folder
    #Not needed but much more elegant
    file(GLOB_RECURSE FIXED_LIBS "@CMAKE_INSTALL_PREFIX@/bin/*@CMAKE_SHARED_LIBRARY_SUFFIX@*")

    foreach(LIB ${FIXED_LIBS})
        file(COPY ${LIB} DESTINATION "@CMAKE_INSTALL_PREFIX@/lib/")
        file(REMOVE ${LIB})
    endforeach()

endif()
